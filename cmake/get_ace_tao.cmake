include(FetchContent)

if(CMAKE_VERSION VERSION_GREATER_EQUAL 3.24)
  cmake_policy(SET CMP0135 NEW)
endif()

set(_OPENDDS_CONFIGURE_ACE_TAO_ARGS)
set(ACE_IS_BEING_BUILT MPC CACHE INTERNAL "")
set(TAO_IS_BEING_BUILT MPC CACHE INTERNAL "")
if(WIN32)
  set(ext "zip")
  if(CMAKE_GENERATOR STREQUAL "Visual Studio 17 2022")
    set(_OPENDDS_MPC_TYPE vs2022)
  elseif(CMAKE_GENERATOR STREQUAL "Visual Studio 16 2019")
    set(_OPENDDS_MPC_TYPE vs2019)
  elseif(CMAKE_GENERATOR STREQUAL "Visual Studio 15 2017")
    set(_OPENDDS_MPC_TYPE vs2017)
  elseif(CMAKE_GENERATOR STREQUAL "Visual Studio 14 2015")
    set(_OPENDDS_MPC_TYPE vc14)
  else()
    set(_OPENDDS_MPC_TYPE vs2022)
  endif()
  set(_OPENDDS_ACE_CONFIG_FILE "config-win32.h")
else()
  set(ext "tar.bz2")
  set(_OPENDDS_MPC_TYPE gnuace)
  if(CMAKE_SYSTEM_NAME STREQUAL Linux)
    set(_OPENDDS_ACE_CONFIG_FILE "config-linux.h")
    list(APPEND _OPENDDS_CONFIGURE_ACE_TAO_ARGS --platform-macros-file "platform_linux.GNU")
  elseif(CMAKE_SYSTEM_NAME STREQUAL Android)
    set(_OPENDDS_ACE_CONFIG_FILE "config-android.h")
    list(APPEND _OPENDDS_CONFIGURE_ACE_TAO_ARGS --platform-macros-file "platform_android.GNU")
    if(NOT DEFINED ANDROID_ABI)
      message(FATAL_ERROR "ANDROID_ABI not defined!")
    endif()
    list(APPEND _OPENDDS_CONFIGURE_ACE_TAO_ARGS --macro-line "android_abi:=${ANDROID_ABI}")
    if(NOT DEFINED ANDROID_PLATFORM)
      message(FATAL_ERROR "ANDROID_PLATFORM not defined!")
    elseif(ANDROID_PLATFORM MATCHES "(android-)?([0-9]+)")
      list(APPEND _OPENDDS_CONFIGURE_ACE_TAO_ARGS --macro-line "android_api:=${CMAKE_MATCH_2}")
    else()
      message(FATAL_ERROR "ANDROID_PLATFORM is in unexpected format: ${ANDROID_PLATFORM}")
    endif()
    if(NOT DEFINED ANDROID_NDK)
      message(FATAL_ERROR "ANDROID_NDK not defined!")
    endif()
    list(APPEND _OPENDDS_CONFIGURE_ACE_TAO_ARGS --macro-line "android_ndk:=${ANDROID_NDK}")
    if(NOT DEFINED OPENDDS_ACE_TAO_HOST_TOOLS)
      if(DEFINED OPENDDS_HOST_TOOLS)
        if(IS_DIRECTORY "${OPENDDS_HOST_TOOLS}/ace_tao/bin")
          set(OPENDDS_ACE_TAO_HOST_TOOLS "${OPENDDS_HOST_TOOLS}/ace_tao" CACHE INTERNAL "")
        else()
          set(OPENDDS_ACE_TAO_HOST_TOOLS "${OPENDDS_HOST_TOOLS}" CACHE INTERNAL "")
        endif()
      else()
        message(FATAL_ERROR "OPENDDS_ACE_TAO_HOST_TOOLS or OPENDDS_HOST_TOOLS need to be defined!")
      endif()
    endif()
    list(APPEND _OPENDDS_CONFIGURE_ACE_TAO_ARGS
      --macro-line "TAO_IDL:=${OPENDDS_ACE_TAO_HOST_TOOLS}/bin/tao_idl"
      --macro-line "TAO_IDLFLAG+=-g ${OPENDDS_ACE_TAO_HOST_TOOLS}/bin/ace_gperf"
      --macro-line "TAO_IDL_DEP:=${OPENDDS_ACE_TAO_HOST_TOOLS}/bin/tao_idl"
    )
  elseif(CMAKE_SYSTEM_NAME STREQUAL Darwin)
    set(_OPENDDS_ACE_CONFIG_FILE "config-macosx.h")
    list(APPEND _OPENDDS_CONFIGURE_ACE_TAO_ARGS --platform-macros-file "platform_macosx.GNU")
  endif()
endif()
if(NOT DEFINED _OPENDDS_MPC_TYPE OR NOT DEFINED _OPENDDS_ACE_CONFIG_FILE)
  message(FATAL_ERROR "Not sure how to configure ACE/TAO for this system "
    "(${CMAKE_SYSTEM_NAME}/${CMAKE_GENERATOR})")
endif()

if(DEFINED OPENDDS_ACE_TAO_SRC)
  if(NOT DEFINED OPENDDS_MPC)
    message(FATAL_ERROR "OPENDDS_ACE_TAO_SRC requires OPENDDS_MPC to be set")
  endif()
  if(EXISTS "${OPENDDS_ACE_TAO_SRC}/ace/Version.h")
    set(OPENDDS_ACE "${OPENDDS_ACE_TAO_SRC}" CACHE INTERNAL "")
  elseif(EXISTS "${OPENDDS_ACE_TAO_SRC}/ACE/ace/Version.h")
    set(OPENDDS_ACE "${OPENDDS_ACE_TAO_SRC}/ACE" CACHE INTERNAL "")
    if(_OPENDDS_MPC_TYPE STREQUAL gnuace)
      set(_OPENDDS_ACE_MPC_NAME_IS_ACE_TARGET TRUE CACHE INTERNAL "")
    endif()
  else()
    message(FATAL_ERROR "Can't find ace/Version.h in OPENDDS_ACE_TAO_SRC")
  endif()
  if(EXISTS "${OPENDDS_ACE_TAO_SRC}/TAO/tao/Version.h")
    set(OPENDDS_TAO "${OPENDDS_ACE_TAO_SRC}/TAO" CACHE INTERNAL "")
  else()
    message(FATAL_ERROR "Can't find tao/Version.h in OPENDDS_ACE_TAO_SRC")
  endif()
  message(STATUS "Using ACE/TAO at ${OPENDDS_ACE}")
else()
  set(OPENDDS_ACE_TAO_SRC "${OPENDDS_BUILD_DIR}/ace_tao")
  set(OPENDDS_ACE "${OPENDDS_ACE_TAO_SRC}" CACHE INTERNAL "")
  set(OPENDDS_MPC "${OPENDDS_ACE}/MPC" CACHE INTERNAL "")
  set(OPENDDS_TAO "${OPENDDS_ACE}/TAO" CACHE INTERNAL "")

  file(STRINGS "${OPENDDS_SOURCE_DIR}/acetao.ini" ace_tao_ini)
  unset(section)
  foreach(line IN LISTS ace_tao_ini)
    if(line MATCHES "^\\[(.*)\\]$")
      set(section "${CMAKE_MATCH_1}")
    elseif(section AND line MATCHES "^([^=#]+)=(.*)$")
      set(name "${CMAKE_MATCH_1}")
      set(value "${CMAKE_MATCH_2}")
      set("${section}-${name}" "${value}")
    endif()
  endforeach()
  set(url "${ace7tao3-${ext}-url}")
  set(md5 "${ace7tao3-${ext}-md5}")

  message(STATUS "Getting ACE/TAO from ${url}")
  FetchContent_Declare(ace_tao_dl
    PREFIX "${OPENDDS_BUILD_DIR}/ace_tao_tmp"
    SOURCE_DIR "${OPENDDS_ACE_TAO_SRC}"
    URL "${url}"
    URL_MD5 "${md5}"
  )
  FetchContent_Populate(ace_tao_dl)
endif()
